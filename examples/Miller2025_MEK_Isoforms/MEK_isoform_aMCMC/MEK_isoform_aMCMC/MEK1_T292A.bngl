begin model
# ==============================================================================
#                                                                              #
#                                                                              #
#            MEK1-MEK2 Heterodimerization-based ERK Cascade Model              #
#           ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~             #
#                                                                              #
#  The model includes the negative feedback from ERK to MEK1 and its mediation #
#  to MEK2 via heterodimerization. The code describes the events taking place  #
#  in WT MEFs.                                                                 #
#                                                                              #
#  This code features the article entitled:                                    #
#                                                                              #
#      "MEK1 and MEK2 differentially control the duration and amplitude        #
#                        of the ERK cascade response"                          #
#                                                                              #
#  by Pawel Kocieniewski and Tomasz Lipniacki (Physical Biology, 2013).	       #
#  Additional work with PyBNF and parameterization by Miller et al. (2025).    #
#                                                                              #
#                                 ...:::*:::...                                #
#                                                                              #
#  The experimental perturbations described in the paper are simulated as      #
#  follows:                                                                    #
#                                                                              #
#   a) MEK1 knock-out -- setting MEK1 content to zero (MEK1_0 = 0);            #
#                                                                              #
#   b) MEK1 dimerization ablation (N78G mutant) -- disabling MEK1 homo- and    #
#      dimerization (set constants b2 = 0 and b4 = 0);                         #
#                                                                              #
#   c) MEK1 negative feedback ablation (T292A mutant) -- disabling MEK1        #
#      Thr292 phosphorylation rule (set constant p4 = 0);                      #
#                                                                              #
#   d) MEK1 constitutive MEK1 repression (T292D mutant):                       #
#                                                                              #
#      i) -- setting total intial MEK1 phosphorylated at Thr292 (replace       #
#         MEK1(d,T292~Y,S1~Y) by MEK1(d,T292~P,S1~Y),                          #
#                                                                              #
#     ii) -- disabling MEK1 Thr292p dephosphorylation (set constant u4 = 0),   #
#                                                                              #
#    iii) -- attenuate MEK1-Thr292p/PHP binding rate to reflect the lower      #
#         affinity of phosphomimetic substitution with respect to Thr 292P     #
#         (set b5 = b5/N, N = 3).                                              #
#                                                                              #
#                                 ...:::*:::...                                #
#                                                                              #
#  Last changes:                                                               #
#                                                                              #
#    *  Ely F. Miller  Fri June 27 12:37:29 PST 2025                           #
#                                                                              #
# ==============================================================================


begin parameters

  ## General --------------------------------------------------------------
  #
  MEK1_fraction      0.67   # MEK1 fraction of total MEKs content
  X                  5      # MEK2-to-MEK1 kinase activity ratio
  sigma sigma__FREE

  ## Concentrations of species ---------------------------------------------
  #
  EGFR_0             4.999382777809e+05   # initial EGFR level, resting cell steady state
  #EGFR_steady		4.999382777809e+05
  SOS1_0             1.999753111124e+05   # initial Sos1 level, resting cell steady state
  #SOS1_steady		1.999753111124e+05
  RAS_0         500000   # initial RAS level, constant  
  RAF_0         500000   # initial RAF level, constant 
  MEK_tot_0     200000   # initial MEK1 and MEK2 total level, constant 
  MEK1_0              MEK_tot_0*MEK1_fraction      # initial MEK1 level, constant 
  MEK1_0_T292p       0                             # initial MEK1-Thr292p level 
  MEK2_0              MEK_tot_0*(1-MEK1_fraction)  # initial MEK2 level, constant 
  ERK_0        3000000   # initial combined ERK1 and ERK2 level, constant 
  PHP_MEK_0    3000000   # initial PHP level, constant 


  ## Assembly and dynamics of the EGFR receptor complexes ------------------
  #  
  s1       2.5     # EGFR receptor subunit constitutive production
  d1        5e-6   # EGFR receptor subunit constitutive degradation
  s2       1       # Sos1 constitutive production
  d2        5e-6   # Sos1 constitutive degradation
  c1       0       # formation of EGFR receptor subunit/ligand complex -- SIGNAL
  c1_init  0       # c1 value prior to stimulation with ligand   
  c1_L     0.02    # c1 value after stimulation with ligand   
  c2 2e-7   # EGFR receptor dimerization due ligand
  t1     100       # EGFR subunits transphosphorylation in EGFR dimer
  d3 d3__FREE       #1e-3   # degradation of ligand-bound dimer complexes
  b1        4e-8   # association of phosphorylated receptor EGFR subunits with Sos1      
  n1        2e-3   # disassociation of EGFR receptor subunits from Sos1


  ## MEK1 and MEK2 homo- and heterodimerization ----------------------------
  #
  b2            1e-5   # MEK1 homodimer formation
  n2            1e-3   # MEK1 dimer dissociation                                   
  b3            1e-5   # MEK2 homodimer formation                    
  n3            3e-2   # MEK2 homodimer dissociation                          
  b4            1e-5   # MEK1 and MEK2 heterodimer formation
  n4            1e-3   # MEK1 and MEK2 heterodimer dissociation            


  ## Signal transduction through RAF, MEK1/2, and ERK ----------------------
  #
  a1 1.5e-7   # activation of Ras by EGFR-Sos1 complex (exchange of GDP for GTP) 
  i1        2e-2   # inactivation of RAS (hydrolysis of bound GTP to GDP)
  a2        4e-8   # activation of RAF by RAS-GTP
  i2        1e-2   # inactivation of RAF
  p1      1.5e-7   # phosphorylation of MEK1 and MEK2 on the activation sites by RAF   
  u1        5e-3   # dephosphorylation of MEK1 and MEK2 on the activation sites
  p2a       1e-6   # phosphorylation of ERK on the activation sites by MEK1
  p2b           X*p2a  # phosphorylation of ERK on the activation sites by MEK2
  u2        2e-2   # dephosphorylation of ERK on the activation sites 


  ## Phosphorylation and dephosphorylation ---------------------------------
  ## of feedback sites of Sos1 and MEK1 (Thr292)
  #
  p3 2e-9   #feedback phosphorylation of Sos1 by active ERK
u3 u3__FREE         #1e-3   #dephosphorylation of the Sos1 feedback site
p4            0   #feedback phosphorylation of MEK1 by ERK
  u4 2e-4    #dephosphorylation of the MEK1 feedback site (Thr292)  

  ## Interactions of PHP with Thr292p of MEK1 ------------------------------
  #
  b5        4e-9   # PHP phosphatse binding to Thr292p of MEK1
  n5        2e-4   # dissociation of PHP phosphatase from Thr292p of MEK1
  u5        20       # dephosphorylation of MEK1 and MEK2 on the activation sites
                   #     by MEK1-bound PHP phosphatase 

end parameters


# ==============================================================================


begin molecule types

  ## EGFR receptor subunit 
  #  ---------------------
  #
  #   lig -- indicates whether subunit is ligand-bound (B) or not (U)
  #   sos -- SOS1 binding domain
  #   dim -- subunit dimerization domain
  #   S   -- indicates whether subunit has undergone phosphorylation 
  #              (P) or not (U) due to subunit transphosphorylation 
  #                 
  EGFR_wt(lig~U~B,sos,dim,S~U~P)


  ## SOS1 Guanine Exchange Factor
  #  ----------------------------
  # 
  #  egfr -- EGFR receptor subunit binding domain. In reality this interaction 
  #              is mediated by Grb2 adapter protein. For sake of simplicity, 
  #              we assume the interaction to be direct.
  #  S -- indicates whether Sos1 has undergone feedback phosphorylation by ERK.
  #              Sos1 possesses at least 4 feedback phosphorlyation sites. For 
  #              simplicity sake we model it as a single site.
  #
  SOS1_wt(egfr,S~U~P)


  ## RAS GTPase
  #  ----------
  #
  #  nt -- indicates whether RAS is bound to GTP (activated) or to GDP 
  #              (inactivated).
  #
  RAS_wt(nt~GDP~GTP)


  ## RAF kinase
  #  ----------
  #
  #  S -- indicates whether RAS is active (A) or inactive (I).
  #
  RAF_wt(S~A~I)


  ## MEK1 kinase
  #  -----------
  #
  #  d  -- MEK1 homo- and heterodimerization domain.
  #  S1 -- indicates whether MEK1 is unphosphorylated (Y), monophosphorylated
  #              (Yp) or double phosphorylated (Ypp). Double phosphorylation 
  #              is considered activating.
  #  T292 -- represents Thr292 residue, which undergoes feedback phosphorylation
  #              (Yp) by ERK. When phosphorylated, it becomes a binding domain 
  #              for PHP phosphatase.    
  #
  MEK1_wt(d,T292~Y~Yp,S1~Y~Yp~Ypp)


  ## MEK2 kinase
  #  -----------
  #
  #  d  -- MEK2 homo- and heterodimerization domain.
  #  S1 -- indicates whether MEK2 is unphosphorylated (Y), monophosphorylated 
  #              (Yp) or double phosphorylated (Ypp). Double phosphorylation 
  #              is considered activating.
  #
  MEK2_wt(d,S1~Y~Yp~Ypp)


  ## ERK
  # -----
  #
  #  S1 -- indicates whether ERK is unphosphorylated (Y), monophosphorylated 
  #              (Yp) or double phosphorylated (Ypp). Double phosphorylation 
  #              is considered activating.
  #
  ERK_wt(S1~Y~Yp~Ypp)


  ## PHP (MEK phosphatase that binds to Thr292p)
  # -----
  #
  #  mek -- MEK1 binding domain at MEK1-Thr292p.
  #
  PHP_wt(mek)


  ## Dummy species to simulate constant de novo synthesis reactions 
  #  (Source) and removal/degradation (Sink).
  #
  Source_wt()
  #
  Sink_wt()

end molecule types


# ==============================================================================

begin species

  EGFR_wt(lig~U,sos,dim,S~U)     EGFR_0
  SOS1_wt(egfr,S~U)              SOS1_0
  RAS_wt(nt~GDP)                  RAS_0
  RAF_wt(S~I)                     RAF_0
  MEK1_wt(d,T292~Y,S1~Y)         MEK1_0
  MEK1_wt(d,T292~Yp,S1~Y)        MEK1_0_T292p
  MEK2_wt(d,S1~Y)                MEK2_0
  ERK_wt(S1~Y)                    ERK_0
  PHP_wt(mek)                 PHP_MEK_0

  #Source_wt()                         1
  #Sink_wt()                           0

end species


# ==============================================================================


begin reaction rules

### WT MODEL

  ## Rule 1. Constitutive production and degradation of EGFR subunit.
  #
  #Source_wt() -> Source_wt() + EGFR_wt(lig~U,sos,dim,S~U)  s1
  #EGFR_wt(sos,dim,S~U) -> Sink_wt()                        d1


  ## Rule 2. Constitutive production and degradation of SOS1.
  #
  #Source_wt() -> Source_wt() + SOS1_wt(egfr,S~U)  s2
  #SOS1_wt(egfr,S~U) -> Sink_wt()                  d2


  ## Rule 3. MEK1 and MEK2 homo- and heterodimerization.
  #
  MEK1_wt(d) + MEK1_wt(d) <-> MEK1_wt(d!1).MEK1_wt(d!1)  b2,n2
  MEK2_wt(d) + MEK2_wt(d) <-> MEK2_wt(d!1).MEK2_wt(d!1)  b3,n3
  MEK2_wt(d) + MEK1_wt(d) <-> MEK2_wt(d!1).MEK1_wt(d!1)  b4,n4


  ## Rule 4. Binding of EGFR receptor subunit with the ligand.
  #
  EGFR_wt(lig~U,dim,sos,S~U) -> EGFR_wt(lig~B,dim,sos,S~U)  c1


  ## Rule 5. Dimerization of ligand-bound receptors.  
  #
  EGFR_wt(lig~B,dim,sos,S~U) + EGFR_wt(lig~B,dim,sos,S~U)   -> \
  EGFR_wt(lig~B,dim!1,sos,S~U).EGFR_wt(lig~B,dim!1,sos,S~U) c2


  ## Rule 6. Transphosphorylation of dimerized EGFR receptor subunits. 
  #
  EGFR_wt(lig~B,dim!+,sos,S~U) -> EGFR_wt(lig~B,dim!+,sos,S~P)  t1


  ## Rule 7. Binding of a dimerized transphosphorylated EGFR receptor 
  #          subunit to Sos1.
  #
  EGFR_wt(lig~B,dim!+,sos,S~P) + SOS1_wt(egfr,S~U)    <->   \
  EGFR_wt(lig~B,dim!+,sos!1,S~P).SOS1_wt(egfr!1,S~U) b1,n1


  ## Rule 8. Degradation of EGFR dimers. 
  #
  EGFR_wt(lig~B,dim!1,sos!?,S~P).EGFR_wt(lig~B,dim!1,sos!?,S~P) -> Sink_wt()  d3


  ## Rule 9. Activation of RAS by EGFR-Sos1 complex.
  #
  EGFR_wt(sos!1,S~P).SOS1_wt(egfr!1,S~U) + RAS_wt(nt~GDP)  ->  \
  EGFR_wt(sos!1,S~P).SOS1_wt(egfr!1,S~U) + RAS_wt(nt~GTP)  a1


  ## Rule 10. Deactivation of RAS.
  #
  RAS_wt(nt~GTP) -> RAS_wt(nt~GDP)  i1


  ## Rule 11. Activation of Raf by activated RAS.
  #
  RAS_wt(nt~GTP) + RAF_wt(S~I)  ->  \
  RAS_wt(nt~GTP) + RAF_wt(S~A)  a2


  ## Rule 12. Raf Deactivation.
  #
  RAF_wt(S~A) -> RAF_wt(S~I)  i2        


  ## Rule 13. Phosphorylation of MEK1 on activating sites by RAF.
  #
  MEK1_wt(S1~Y)   + RAF_wt(S~A)  ->  \
  MEK1_wt(S1~Yp)  + RAF_wt(S~A) 2*p1
  #  
  MEK1_wt(S1~Yp)  + RAF_wt(S~A)  ->  \
  MEK1_wt(S1~Ypp) + RAF_wt(S~A)  p1


  ## Rule 14. Dephosphorylation of MEK1 on activating sites.
  #
  MEK1_wt(S1~Ypp) -> MEK1_wt(S1~Yp)  2*u1     
  #
  MEK1_wt(S1~Yp)  -> MEK1_wt(S1~Y)    u1 


  ## Rule 15. Phosphorylation of MEK2 on activating sites by RAF.
  #
  MEK2_wt(S1~Y)   + RAF_wt(S~A)  ->  \
  MEK2_wt(S1~Yp)  + RAF_wt(S~A) 2*p1
  #
  MEK2_wt(S1~Yp)  + RAF_wt(S~A)  ->  \
  MEK2_wt(S1~Ypp) + RAF_wt(S~A)  p1


  ## Rule 16. Dephosphorylation of MEK2 on activating sites.
  #
  MEK2_wt(S1~Ypp) -> MEK2_wt(S1~Yp) 2*u1     
  #
  MEK2_wt(S1~Yp)  -> MEK2_wt(S1~Y)   u1


  ## Rule 17. Activation of ERK by MEK1.
  #
  ERK_wt(S1~Y)   + MEK1_wt(S1~Ypp)  ->  \
  ERK_wt(S1~Yp)  + MEK1_wt(S1~Ypp) 2*p2a
  #
  ERK_wt(S1~Yp)  + MEK1_wt(S1~Ypp)  ->  \
  ERK_wt(S1~Ypp) + MEK1_wt(S1~Ypp)  p2a


  ## Rule 18. Activation of ERK by MEK2.
  #
  ERK_wt(S1~Y)   + MEK2_wt(S1~Ypp)  ->   \
  ERK_wt(S1~Yp)  + MEK2_wt(S1~Ypp)  2*p2b
  #
  ERK_wt(S1~Yp)  + MEK2_wt(S1~Ypp)   ->  \
  ERK_wt(S1~Ypp) + MEK2_wt(S1~Ypp)  p2b


  ## Rule 19. Dephosphorylation of ERK on activating sites.
  #
  ERK_wt(S1~Ypp) -> ERK_wt(S1~Yp) 2*u2 
  #
  ERK_wt(S1~Yp)  -> ERK_wt(S1~Y)   u2


### NEGATIVE FEEDBACK BY ERK

  ## Rule 20. Feedback phosphorylation of Sos1 by ERK.
  #
  SOS1_wt(egfr,S~U) + ERK_wt(S1~Ypp)  ->  \
  SOS1_wt(egfr,S~P) + ERK_wt(S1~Ypp)  p3

  ## Rule 21. Dephosphorylation of the ERK-mediated negative feedback 
  #           site of Sos1.
  #
  SOS1_wt(egfr,S~P) -> SOS1_wt(egfr,S~U)  u3


  ## Rule 22. Feedback Phosphorylation of MEK1 by ERK on Thr292.
  #
  MEK1_wt(T292~Y) + ERK_wt(S1~Ypp)  ->  \ 
  MEK1_wt(T292~Yp)+ ERK_wt(S1~Ypp)  p4


  ## Rule 23. Dephosphorylation of MEK1 on the negative feedback site (Thr292).
  #
  MEK1_wt(T292~Yp) -> MEK1_wt(T292~Y)  u4


  ## Rule 24. PHP Attachment to phosphorylated Thr292 of MEK1.
  #
  MEK1_wt(T292~Yp) + PHP_wt(mek)    <->  \
  MEK1_wt(T292~Yp!1).PHP_wt(mek!1) b5,n5
 

  ## Rule 25. Dephosphorylation of MEK1 on activating sites by the bound PHP.
  #
  MEK1_wt(T292~Yp!1,S1~Ypp).PHP_wt(mek!1) ->  \
  MEK1_wt(T292~Yp!1,S1~Yp).PHP_wt(mek!1) 2*u5
  #
  MEK1_wt(T292~Yp!1,S1~Yp).PHP_wt(mek!1)  ->  \
  MEK1_wt(T292~Yp!1,S1~Y).PHP_wt(mek!1)   u5         


  ## Rule 26. Dephosphorylation of the activating sites of MEK1 and MEK2 
  #           in the dimer with PHP-bound MEK1.
  #
  MEK1_wt(d!1,T292~Yp!+).MEK1_wt(d!1,S1~Ypp) ->  \
  MEK1_wt(d!1,T292~Yp!+).MEK1_wt(d!1,S1~Yp) 2*u5
  #
  MEK1_wt(d!1,T292~Yp!+).MEK1_wt(d!1,S1~Yp)  ->  \
  MEK1_wt(d!1,T292~Yp!+).MEK1_wt(d!1,S1~Y)   u5
  #
  MEK1_wt(d!1,T292~Yp!+).MEK2_wt(d!1,S1~Ypp) ->  \
  MEK1_wt(d!1,T292~Yp!+).MEK2_wt(d!1,S1~Yp) 2*u5
  #
  MEK1_wt(d!1,T292~Yp!+).MEK2_wt(d!1,S1~Yp)  ->  \
  MEK1_wt(d!1,T292~Yp!+).MEK2_wt(d!1,S1~Y)   u5

end reaction rules


# ==============================================================================


begin observables

# ## Activity --------------------------------------------------------------
# #
# Species  EGFR_Lig_wt                 EGFR_wt(lig~B)
# Species  EGFR_SOS_complex_wt         EGFR_wt(sos!1,S~P).SOS1_wt(egfr!1,S~U)
# Species  RAS_active_WT               RAS_wt(nt~GTP)
# Species  RAF_active_WT               RAF_wt(S~A)
# Species  MEK1_active_WT              MEK1_wt(S1~Ypp)
# Species  MEK2_active_WT              MEK2_wt(S1~Ypp)
# Species  MEK_total_active_WT         MEK1_wt(S1~Ypp) MEK2_wt(S1~Ypp)
# Species  ERK_active_WT               ERK_wt(S1~Ypp) 
#
#
# ## Negative feedback -----------------------------------------------------
# Species  SOS1_P_wt                   SOS1_wt(egfr,S~P)
# Species  MEK1_T292p_wt               MEK1_wt(T292~Yp!?)
# Species  MEK1_Phosphatase_Complex_wt MEK1_wt(T292~Yp!+)  
#
#
# ## Species levels --------------------------------------------------------
# #
# Species EGFR_tot_wt                  EGFR_wt()
# Species EGFR_mono_tot_wt             EGFR_wt(dim)
# Species EGFR_dim_tot_wt              EGFR_wt(dim!1).EGFR_wt(dim!1)
# Species EGFR_p_wt                    EGFR_wt(S~P)
# Species Sos1_tot_wt                  SOS1_wt()
# Species RAS_tot_wt                   RAS_wt()
# Species RAF_tot_wt                   RAF_wt()
# Species MEK1_tot_wt                  MEK1_wt()
# Species MEK2_tot_wt                  MEK2_wt()
# Species ERK_tot                      ERK_wt()
#
#
# ## Dimer levels ----------------------------------------------------------
# #
# Molecules MEK1_homo_dim_wt           MEK1_wt(d!1).MEK1_wt(d!1)
# Molecules MEK2_homo_dim_wt           MEK2_wt(d!1).MEK2_wt(d!1)
# Molecules MEK_hetero_dim_wt          MEK1_wt(d!1).MEK2_wt(d!1)


  ## Publication observables -----------------------------------------------
  #
  Species  pSos1_wt                    SOS1_wt(S~P)
  Species  pEGFR_wt                    EGFR_wt(S~P)
  Species  pERK1_2_wt                  ERK_wt(S1~Ypp) 
  #
  Species  MEK1_pRDS_wt                MEK1_wt(S1~Ypp)
  Species  MEK2_pRDS_wt                MEK2_wt(S1~Ypp)
  Species  MEK_pRDS_T292A                 MEK1_wt(S1~Ypp) MEK2_wt(S1~Ypp)

end observables

end model
# ==============================================================================


#generate_network({overwrite=>1});
#writeMfile();


setParameter("c1",0.02);
simulate({suffix=>"T292A",method=>"ode",t_end=>3600,n_steps=>12});

# ==============================================================================
# EOF



